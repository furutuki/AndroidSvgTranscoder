plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.dokka' version '1.4.32'
    id 'com.gradle.plugin-publish' version '1.1.0'
}

dependencies {
    implementation 'io.github.furutuki:transcoder-core:0.1.1'
}

pluginBundle {
    website = 'https://github.com/furutuki/AndroidSvgTranscoder'
    vcsUrl = 'https://github.com/furutuki/AndroidSvgTranscoder.git'
    tags = ['svg', 'transcode', 'android', 'java']
}

group = 'io.github.furutuki'
version = '0.1.1'

gradlePlugin {
    plugins {
        svgTranscoderPlugin {
            id = 'io.github.furutuki.android-svg-transcoder'
            displayName = 'AndroidSvgTranscoder plugin'
            description = 'A gradle plugin that helps you transcode svg files to android source file(Java),' +
                    ' with which you can compile and use the generated classes to render svg image ' +
                    'in canvas'
            implementationClass = 'com.github.furutuki.androidsvgtranscoder.plugin.SvgTranscoderPlugin'
        }
    }
}

task generateSourcesJar(type: Jar) {
    group ='jar'
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task generateDocJar(type: Jar, dependsOn: dokkaHtml) {
    group = 'jar'
    archiveClassifier.set('javadoc')
    from "$buildDir/dokka/html"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.furutuki'
            artifactId = 'transcoder-plugin'
            version = '0.1.1'

            from components.java

            pom {
                name = 'AndroidSvgTranscoder plugin library'
                description = 'Android SVG Transcoder plugin library, that makes you able to transcode SVG file to Android source file(Java).'
                url = 'https://github.com/furutuki/AndroidSvgTranscoder'
                inceptionYear = '2022'

                scm {
                    url = 'https://github.com/furutuki/AndroidSvgTranscoder'
                    connection = 'scm:git@https://github.com/furutuki/AndroidSvgTranscoder.git'
                    developerConnection = 'scm:git@https://github.com/furutuki/AndroidSvgTranscoder.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        comments = 'A business-friendly OSS license'
                    }
                }

                developers {
                    developer {
                        id = 'furutuki'
                        name = 'HU Zhong'
                        email = 'furutuki@foxmail.com'
                        url = 'https://github.com/furutuki'
                    }
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/furutuki/AndroidSvgTranscoder/issues'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'MavenCentralRepo'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials(PasswordCredentials)
        }
        maven {
            name = 'localRepo'
            url = "../localRepo"
        }
    }
}
signing {
    sign publishing.publications.maven
}